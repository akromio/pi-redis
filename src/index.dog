use (
  {createClient} = alias://redis
)

export {
  plugin = "redis"
  desc = "Akromio plugin for working with Redis."
  tags = []
  ini = initialize
  fin = finalize
  ops =
    ping = use("./ops/ping")
    'xgroup.create' = use("./ops/xgroupcreate")
}

/**
 * Initializes the plugin, returning its state.
 */
async fn initialize(args) -> state: map
  # (1) create Redis client
  const client = createClient({
    socket =
      host = args.host ?? "localhost"
      port = args.port ?? 6379
    if args.username then username = args.username
    if args.password then password = args.password
    readonly = args.readonly ?? false
  })

  # (2) connect client
  await(client.connect())

  # (3) set state
  state.{redis = client}

/**
 * Finalizes the plugin.
 */
async proc finalize(state:> {redis})
  pawait(redis.disconnect())
